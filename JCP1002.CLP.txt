/*-------------------------------------------------------------------*/         
/*                                                                   */         
/* Create PDF - Mail BRMS Reports                                    */         
/*   Rename PDF:    No                                               */         
/*   Move PDF:      No                                               */         
/*   Chg Authority: No                                               */         
/*   Manage Spool:  No                                               */         
/*   Email:         Yes. Single Address In Data Area                 */         
/*                                                                   */         
/*   Data Queue:    frm  lng variable  text                          */         
/*                    1  752 &PDFQUE   Full PDF data queue           */         
/*                   15   26           Qualified job name            */         
/*                   15   10 &JOBNAM   Job name                      */         
/*                   25   10 &USER     User name                     */         
/*                   35    6 &JOBNBR   Job number                    */         
/*                   41   10 &FILE     Spool file name               */         
/*                   53    4 &SPLNBRB  Spool file number (binary)    */         
/*                   57  250           Mail tag (not used)           */         
/*                  307  340           From Path and name            */         
/*             i.e. /DocArchive/BRMS REPORTS/BRM_MAINT/SEROPR/       */         
/*                  463117_000023_QP1A2RCY_08042009_000001.PDF       */         
/*                                                                   */         
/* CC27455 08.31.2009 Charlie McLean                                 */         
/*         Project started                                           */         
/* CC27537 09.28.2009 Charlie McLean                                 */         
/*         Chg'd from address to same as to address                  */         
/*                                                                   */         
/*-------------------------------------------------------------------*/         
                                                                                
 START:      PGM                                                                
                                                                                
             DCLF       FILE(FCP1002)           /* 30 PDF names/parms*/         
                                                                                
/* set the number of attachments per email here                      */         
             DCL        &NA      *INT  LEN(2) VALUE(6)                          
                                                                                
             DCL        &FLDLEN  *DEC  LEN(5 0) VALUE(752)                      
             DCL        &PDFQUE  *CHAR LEN(752) /* PDF Data queue    */         
             DCL        &JOBNAM  *CHAR LEN(10)  /* job name          */         
             DCL        &USER    *CHAR LEN(10)  /* user              */         
             DCL        &JOBNBR  *CHAR LEN(6)   /* job number        */         
             DCL        &FILE    *CHAR LEN(10)  /* spool file name   */         
             DCL        &SPLNBRB *CHAR LEN(4)   /* splf nbr binary   */         
             DCL        &SPLNBRD *DEC  LEN(6)   /* splf nbr decimal  */         
             DCL        &MAILTG  *CHAR LEN(250) /* mail tag          */         
             DCL        &PATHNM  *CHAR LEN(340) /* old path and name */         
             DCL        &TOQUE   *CHAR LEN(10)  /* move to outq name */         
                                                                                
             DCL        &PATHN   *CHAR LEN(200) /* null              */         
             DCL        &PATH1   *CHAR LEN(200) /* path part 1       */         
             DCL        &PATH2   *CHAR LEN(200) /* path part 2       */         
             DCL        &PATH3   *CHAR LEN(200) /* path part 3       */         
             DCL        &PATH4   *CHAR LEN(200) /* path part 4       */         
             DCL        &OLDPTH  *CHAR LEN(256) /* complete path     */         
             DCL        &OLDNAM  *CHAR LEN(200) /* old PDF name      */         
             DCL        &PARMS   *DEC  LEN(3 0) VALUE(5) +                      
                          /* number of parms for PARSE               */         
                                                                                
             DCL        &WNAM    *CHAR LEN(128) /* PDF name frm QUEUE*/         
             DCL        &NEWNAM  *CHAR LEN(128) /* new PDF name      */         
             DCL        &NEWERR  *CHAR LEN(128) /* new if error      */         
             DCL        &TODIR   *CHAR LEN(256) +                               
                 VALUE('/DocArchive/Not used here/')                            
                                                                                
             DCL        &WAIT    *DEC  LEN(5 0) VALUE(1)                        
                                                                                
             DCL        &IN03    *CHAR LEN(1)   /* delete flag       */         
             DCL        &FN      *INT  LEN(2)   /* PDF counter       */         
             DCL        &FT      *CHAR LEN(2)   /* PDF counter text  */         
             DCL        &ADDRESS *CHAR LEN(256) /* email address     */         
                                                                                
             DCL &SIDAT     *CHAR LEN(25) VALUE(*TODAY)                         
             DCL &CTRY      *CHAR LEN(2)                                        
             DCL &FFMT      *CHAR LEN(8)  VALUE(*SYSVAL)                        
             DCL &OFSET     *CHAR LEN(6)  VALUE(*NONE)                          
             DCL &INTVL     *CHAR LEN(8)  VALUE(*DAY)                           
             DCL &TFMT      *CHAR LEN(8)  VALUE(*YEAR4)                         
             DCL &YEAR      *CHAR LEN(6)                                        
             DCL &ERROR     *CHAR LEN(1)                                        
                                                                                
             CALLSUBR   SUBR(S999) /* initial setup                  */         
                                                                                
/* read dataq for next PDF to process                                */         
             CHGVAR     VAR(&FN) VALUE(1)                                       
 OMT:        CALL       PGM(QRCVDTAQ) PARM(CP1002 QGPL &FLDLEN +                
                          &PDFQUE &WAIT)                                        
             IF         COND(&FN = 1 *AND &PDFQUE = ' ') THEN(GOTO +            
                          CMDLBL(END))                                          
             IF         COND(&PDFQUE = ' ') THEN(DO)                            
             CALLSUBR   SUBR(S004) /* send last email                */         
             GOTO       CMDLBL(END)                                             
             ENDDO                                                              
                                                                                
             CALLSUBR   SUBR(S001) /* parse data queue fields        */         
                                                                                
/*           CALLSUBR   SUBR(S002) /* rename PDF and move            */         
                                                                                
/* add PDF path/name to parm list held in FCP1002 file               */         
             CHGVAR     VAR(&FT) VALUE(&FN)                                     
             CALL       PGM(PCP1002) PARM(&FT &PATHNM &IN03)                    
             CHGVAR     VAR(&IN03) VALUE(' ')                                   
                                                                                
/*           CALLSUBR   SUBR(S003) /* move spool to active printer   */         
                                                                                
             IF         COND(&FN = &NA) THEN(DO)                                
             CALLSUBR   SUBR(S004) /* send email                     */         
             CHGVAR     VAR(&FN) VALUE(0)                                       
             CHGVAR     VAR(&IN03) VALUE(1)    /* delete flag        */         
             ENDDO                                                              
             CHGVAR     VAR(&PDFQUE) VALUE(' ')                                 
             CHGVAR     VAR(&FN) VALUE(&FN + 1)                                 
             GOTO       CMDLBL(OMT)                                             
                                                                                
                                                                                
/*-------------------------------------------------------------------*/         
/* S999 - initial setup                                              */         
/*                                                                   */         
             SUBR       SUBR(S999)                                              
/*           CHGJOB     LOG(4 0 *SECLVL) LOGCLPGM(*YES)              */         
             CHGJOB     LOG(0 99 *NOLIST) LOGCLPGM(*NO)                         
             CLRPFM     FILE(FCP1002)                                           
/* get email address for sending                                     */         
             RTVDTAARA  DTAARA(CP10021) RTNVAR(&ADDRESS)                        
/* get year value, ie 2009                                           */         
             CALL X0027 PARM(&SIDAT &CTRY &FFMT &OFSET &INTVL &YEAR +           
                          &TFMT &ERROR)                                         
             CHGVAR     VAR(&TODIR) VALUE(&TODIR *TCAT &YEAR)                   
 E999:       ENDSUBR                                                            
/*-------------------------------------------------------------------*/         
/* S001 - parse standard fields from PDF data queue                  */         
/*                                                                   */         
             SUBR       SUBR(S001)                                              
             CHGVAR     VAR(&JOBNAM)  VALUE(%SST(&PDFQUE 15   10))              
             CHGVAR     VAR(&USER)    VALUE(%SST(&PDFQUE 25   10))              
             CHGVAR     VAR(&JOBNBR)  VALUE(%SST(&PDFQUE 35    6))              
             CHGVAR     VAR(&FILE)    VALUE(%SST(&PDFQUE 41   10))              
             CHGVAR     VAR(&SPLNBRB) VALUE(%SST(&PDFQUE 53    4))              
             CHGVAR     VAR(&MAILTG)  VALUE(%SST(&PDFQUE 57  250))              
             CHGVAR     VAR(&PATHNM)  VALUE(%SST(&PDFQUE 307 340))              
             CHGVAR     VAR(&SPLNBRD) VALUE(%BIN(&SPLNBRB))                     
 E001:       ENDSUBR                                                            
/*-------------------------------------------------------------------*/         
/* S002 - rename PDF based on value in mail tag                      */         
/*        move PDF to single location                                */         
/*        change authority of *PUBLIC to PDF                         */         
/*                                                                   */         
             SUBR       SUBR(S002)                                              
/* new PDF name is first 17 characters of mail tag                   */         
             CHGVAR     VAR(&WNAM)   VALUE(%SST(&MAILTG  1   17))               
             CHGVAR     VAR(&NEWNAM) VALUE(&WNAM *TCAT '.PDF')                  
             CHGVAR     VAR(&NEWERR) VALUE(&WNAM *TCAT '-ERROR.PDF')            
/* get PDF old name w/TAATOOL PARSE                                  */         
             PARSE      INPVAL(&PATHNM) SEPARATOR('/') +                        
                          RTNCNT(&PARMS) RTNVAL1(&PATHN) +                      
                          RTNVAL2(&PATH1) RTNVAL3(&PATH2) +                     
                          RTNVAL4(&PATH3) RTNVAL5(&PATH4) +                     
                          RTNVAL6(&OLDNAM)                                      
/* build up old path name from parse                                 */         
             CHGVAR     VAR(&OLDPTH) VALUE('\' *TCAT &PATH1 *TCAT +             
                          '\' *TCAT &PATH2 *TCAT '\' *TCAT &PATH3 +             
                          *TCAT '\' *TCAT &PATH4)                               
/* rename PDF                                                        */         
             CD         DIR(&OLDPTH)                                            
             MONMSG     MSGID(CPFA0A9)                                          
             RNM        OBJ(&OLDNAM) NEWOBJ(&NEWNAM)                            
             MONMSG     MSGID(CPFA0A9)          /* object not found  */         
             MONMSG     MSGID(CPFA0A0) EXEC(DO) /* object exists     */         
             RNM        OBJ(&OLDNAM) NEWOBJ(&NEWERR)                            
             MONMSG     MSGID(CPFA0A0)                                          
             MONMSG     MSGID(CPFA0A9)                                          
             ENDDO                                                              
/* change *PUBLIC authority to read only                             */         
             CHGAUT     OBJ(&NEWNAM) USER(*PUBLIC) DTAAUT(*R)                   
             MONMSG     MSGID(CPF0000 CPE0000 CPD0000 CPFA0A9)                  
             CHGAUT     OBJ(&NEWERR) USER(*PUBLIC) DTAAUT(*R)                   
             MONMSG     MSGID(CPF0000 CPE0000 CPD0000 CPFA0A9)                  
/* move new PDF to /DocArchive/Invoices by year/2009                 */         
             MOV        OBJ(&NEWNAM) TODIR(&TODIR)                              
             MONMSG     MSGID(CPFA0A0)                                          
             MONMSG     MSGID(CPFA0A9) EXEC(DO) /* object not found  */         
/* if error move error PDF                                           */         
             MOV        OBJ(&NEWERR) TODIR(&TODIR)                              
             MONMSG     MSGID(CPFA0A0)                                          
             MONMSG     MSGID(CPFA0A9)                                          
             ENDDO                                                              
 E002:       ENDSUBR                                                            
/*-------------------------------------------------------------------*/         
/* S003 - move spool to active printer                               */         
/*                                                                   */         
             SUBR       SUBR(S003)                                              
             CHGVAR     VAR(&TOQUE) VALUE('PRT11I')                             
/* move the spool file to a real printer and make ready              */         
             CHGSPLFA   FILE(&FILE) JOB(&JOBNBR/&USER/&JOBNAM) +                
                          SPLNBR(&SPLNBRD) OUTQ(&TOQUE)                         
             MONMSG     MSGID(CPF3344) /* no longer in system        */         
             MONMSG     MSGID(CPF3341) /* file not changed           */         
             RLSSPLF    FILE(&FILE) JOB(&JOBNBR/&USER/&JOBNAM) +                
                          SPLNBR(&SPLNBRD)                                      
             MONMSG     MSGID(CPF3344) /* no longer in system        */         
 E003:       ENDSUBR                                                            
/*-------------------------------------------------------------------*/         
/* S004 - send email with attachments                                */         
/*        single address                                             */         
/*                                                                   */         
             SUBR       SUBR(S004)                                              
             RCVF       RCDFMT(ICP1002)                                         
             MONMSG     MSGID(CPF0864) /* end of file */                        
             EMLSTMF    SUBJECT('Daily BRMS Reports') +                         
                          FROMNAME('SEROPR') +                                  
/* CC27537 */             FROMADDR(&ADDRESS) +                                  
                          TO(&ADDRESS/'Recovery Guy'/*TO) +                     
                          STMF(&STMF01 &STMF02 &STMF03 &STMF04 +                
                          &STMF05 &STMF06 &STMF07 &STMF08 &STMF09 +             
                          &STMF10 &STMF11 &STMF12 &STMF13 &STMF14 +             
                          &STMF15 &STMF16 &STMF17 &STMF18 &STMF19 +             
                          &STMF20 &STMF21 &STMF22 &STMF23 &STMF24 +             
                          &STMF25 &STMF26 &STMF27 &STMF28 &STMF29 +             
                          &STMF30) MSGM(JDEOBJP/SERSRC/CP1002.01)               
 E004:       ENDSUBR                                                            
/*-------------------------------------------------------------------*/         
/*                                                                   */         
/*-------------------------------------------------------------------*/         
                                                                                
END:         ENDPGM                                                             
